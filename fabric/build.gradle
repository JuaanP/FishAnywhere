plugins {
    id 'multiloader-loader'
    id 'fabric-loom' version '1.6-SNAPSHOT'
    id 'com.modrinth.minotaur'
    id 'com.matthewprenger.cursegradle'
}

base {
    archivesName = "${mod_id}-fabric-${minecraft_version}-v"
}

repositories {
    maven {url "https://maven.terraformersmc.com/releases"}
    maven {url "https://maven.nucleoid.xyz/"}
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
}

loom {
    def aw = project(":common").file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("runs/client")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("runs/server")
        }
    }
}

tasks.register('copyJarToBuild') {
    dependsOn 'remapJar'
    doLast {
        copy {
            from remapJar.archiveFile
            into rootProject.file("builds/${minecraft_version}")
        }
    }
}

build.finalizedBy('copyJarToBuild')

// Configuración de Modrinth
modrinth {
    token = rootProject.ext.modrinth_token
    projectId = rootProject.ext.modrinth_id
    versionNumber = "${version}"
    versionType = "release" // release, beta, alpha
    uploadFile = remapJar
    gameVersions = ["${minecraft_version}"]
    loaders = ["fabric"]
    changelog = "Changelog for version ${version}"
    dependencies {
        required.project "fabric-api"
    }
}

// Configuración de CurseForge
curseforge {
    apiKey = rootProject.ext.curseforge_token
    project {
        id = rootProject.ext.curseforge_id
        releaseType = "release" // release, beta, alpha
        changelogType = "markdown"
        changelog = "Changelog for version ${version}"
        addGameVersion minecraft_version
        addGameVersion "Fabric"
        mainArtifact(remapJar) {
            displayName = "SeamlessTrading-${version}-fabric-${minecraft_version}"
        }
        relations {
            requiredDependency 'fabric-api'
        }
    }
}

tasks.modrinth.dependsOn(tasks.remapJar)
tasks.curseforge.dependsOn(tasks.remapJar)