plugins {
    id 'multiloader-loader'
    id 'fabric-loom' version '1.8.6'
}

base {
    archivesName = "${mod_id}-fabric-${minecraft_version}"
}

repositories {
    maven {url "https://maven.terraformersmc.com/releases"}
    maven {url "https://maven.nucleoid.xyz/"}
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
}

// Deshabilitar explÃ­citamente las tareas relacionadas con sources y javadoc
tasks.configureEach { task ->
    if (task.name.contains('sourcesJar') ||
            task.name.contains('remapSourcesJar') ||
            task.name.contains('javadoc')) {
        task.enabled = false
    }
}

loom {
    def aw = project(":common").file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("runs/client")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("runs/server")
        }
    }
}

// Configurar el JAR para que no tenga el sufijo -dev
tasks.remapJar {
    archiveClassifier = null
}

// Asegurar que el JAR principal sea el remapJar
tasks.build.dependsOn tasks.remapJar